buildscript {
    repositories {
        jcenter()
    }
}

apply plugin: 'com.bmuschko.docker-remote-api'

import com.bmuschko.gradle.docker.tasks.*
import com.bmuschko.gradle.docker.tasks.image.*

def uname_r = "uname -r".execute().text.trim()
def uname_m = "uname -m".execute().text.trim()

task dockerInfo(type: DockerInfo) {
    onNext {
        logger.quiet "Docker Info retreived for project ${project.name}:"
        logger.quiet "  OSType = ${it.osType}"
        logger.quiet "  Architecture = ${it.architecture}"
        owner.ext.dockerInfo = it
    }
}

task dockerBuildImage(type: DockerBuildImage) {
    def prefix = findProperty('dockerImagePrefix') ?: 'openwhisk'
    def tag = findProperty('dockerImageTag') ?: 'latest'
    tags = [    "apigateway-profiling:${tag}",
                "${prefix}/apigateway-profiling:${tag}"
            ] as String[]
    buildArgs = [ 'UNAME_R': uname_r, 'UNAME_M': uname_m ]
    inputDir = new File(buildDir, 'docker')
}

task copyFilesForImage(type: Copy) {
    destinationDir = dockerBuildImage.inputDir
    into('.') {
        from(new File(rootDir, 'common')) {
            include 'init.sh'
            include 'build_config_supervisor.sh'
            include 'etc-api-gateway/**'
            include 'build-scripts/**'
        }
        from('./Dockerfile')
    }

    into("linux-headers-${uname_r}") {
        from("/usr/src/linux-headers-${uname_r}")
    }
}

task copyApiGatewayConf(type: Copy, dependsOn: [copyFilesForImage, dockerInfo]) {
    from(new File(rootDir, 'common/api-gateway.conf.in')) {
        expand('worker_processes':'1',
            'pcre_jit': { dockerInfo.dockerInfo.architecture == 's390x' ? 'no':'yes' })
        rename { 'api-gateway.conf' }
    }
    into new File(dockerBuildImage.inputDir,'etc-api-gateway')
}
dockerBuildImage.dependsOn(copyApiGatewayConf)


